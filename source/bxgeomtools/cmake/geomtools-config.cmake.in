# - Config file for the geomtools package
# It defines the following variables
#  geomtools_INCLUDE_DIRS - include directories for geomtools
#  geomtools_LIBRARY      - libraries to link against
#  geomtools_BIO_LIBRARY  - library targets to link against (Boost I/O library)
#  geomtools_REFLECTION_LIBRARY - library targets to link against (reflection library)
#  geomtools_LIBRARIES    - libraries to link against
#  geomtools_WITH_BIO     - flag for Boost/Serialization I/O DLL (a.k.a. 'bio')
#  geomtools_WITH_REFLECTION  - flag for reflection DLL

#-----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#-----------------------------------------------------------------------

set(geomtools_WITH_BIO        @GEOMTOOLS_WITH_BIO@)
set(geomtools_WITH_REFLECTION @GEOMTOOLS_WITH_REFLECTION@)
set(geomtools_MYGSL_DIR       @mygsl_DIR@)

#-----------------------------------------------------------------------
# Locate ourselves, since all our config files should have been installed
# alongside us...
#
get_filename_component(_geomtools_thisdir "${CMAKE_CURRENT_LIST_FILE}" PATH)

#-----------------------------------------------------------------------
# Include the file listing all the imported targets to make them available
# This is installed in the same location as us...
# Protect it with an in-memory variable so that we don't include
# it twice should multiple calls to find_package be made
if(NOT geomtools_LIBDEPS_LOADED)
  find_package(mygsl REQUIRED CONFIG PATHS ${geomtools_MYGSL_DIR} NO_DEFAULT_PATH)
  include("${_geomtools_thisdir}/geomtools-libraries.cmake")
  set(geomtools_LIBDEPS_LOADED 1)
endif()

#-----------------------------------------------------------------------
# Set variables pointing to used third party headers and locations.
# We have to set the headers because geomtools exports the headers in
# its public interface.
# The locations are set so that clients using those packages (i.e. Boost and
# ROOT), can re-find them if needed using exact locations (EXPERIMENTAL)
# e.g:
#
#  find_package(geomtools)
#  set(BOOST_ROOT ${geomtools_BOOST_ROOT})
#  find_package(Boost NO_DEFAULT_PATH)
#
set(geomtools_THIRD_PARTY_INCLUDE_DIRS
  @mygsl_INCLUDE_DIRS@
  @CLHEP_INCLUDE_DIRS@
  )

if(geomtools_WITH_REFLECTION)
  list(INSERT geomtools_THIRD_PARTY_INCLUDE_DIRS 0
     @CAMP_INCLUDE_DIR@
  )
endif()

#-----------------------------------------------------------------------
# Set geomtools definitions, include paths and library variables
#
get_filename_component(_geomtools_inc "${_geomtools_thisdir}/@TO_GEOMTOOLS_INCLUDEDIR@" ABSOLUTE)

set(geomtools_INCLUDE_DIR "${_geomtools_inc}" CACHE PATH "Path to geomtools headers")

set(geomtools_INCLUDE_DIRS
  ${geomtools_INCLUDE_DIR}
  ${geomtools_THIRD_PARTY_INCLUDE_DIRS}
  )

#-----------------------------------------------------------------------
# Set geomtools_LIBRARIES variable
set(geomtools_LIBRARY geomtools CACHE PATH "Path to geomtools library")

if(geomtools_WITH_BIO)
  set(geomtools_BIO_LIBRARY geomtools_bio)
endif()

if(geomtools_WITH_REFLECTION)
  set(geomtools_REFLECTION_LIBRARY geomtools_reflection)
endif()
  
set(geomtools_LIBRARIES ${geomtools_LIBRARY})

unset(_geomtools_inc)
# end
