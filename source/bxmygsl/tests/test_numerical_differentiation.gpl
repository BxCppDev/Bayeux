# -*- mode: gnuplot; -*- 
# mygsl - test_numerical_differentiation.gpl

set title "mygsl - test_numerical_differentiation"
set grid
set xrange [0:*]
set yrange [-0.25:+1.5]

pow(x,y) = x**y

p(x) = x * (1 - x * (1 - x * (1 - x * (1 - x * (1 - x * (1 - x))))))
dp(x) = 1 - 2 * x + 3 * pow (x, 2) - 4 * pow (x, 3) + 5 * pow (x, 4) - 6 * pow (x, 5) + 7 * pow (x, 6);

! test_numerical_differentiation > tnd.data

set yrange [-0.1:+1.1]
plot \
     p (x) title "Analytic" with lines , \
     'tnd.data' index 0 using 1:2 title "p (x)" with points pt 6 ps 0.5
pause -1 "Hit return..."

set yrange [0.:+5.]
plot \
     dp (x) title "Analytic" with lines , \
     'tnd.data' index 0 using 1:3 title "dp (x)" with points pt 6 ps 0.5, \
     ''         index 1 using 1:2 title "forward (1)" with points pt 6 ps 0.5, \
     ''         index 2 using 1:2 title "backward (1)" with points pt 6 ps 0.5, \
     ''         index 3 using 1:2 title "central (2)" with points pt 6 ps 0.5, \
     ''         index 4 using 1:2 title "central (4)" with points pt 6 ps 0.5
#
pause -1 "Hit return..."
     
plot \
     dp (x) title "Analytic" with lines , \
     'tnd.data' index 1 using 1:2 title "forward (1)" with points pt 6 ps 0.5, \
     ''         index 5 using 1:2 title "forward (2)" with points pt 6 ps 0.5, \
     ''         index 6 using 1:2 title "forward (3)" with points pt 6 ps 0.5, \
     ''         index 2 using 1:2 title "backward (1)" with points pt 6 ps 0.5, \
     ''         index 7 using 1:2 title "backward (2)" with points pt 6 ps 0.5, \
     ''         index 8 using 1:2 title "backward (3)" with points pt 6 ps 0.5

#
pause -1 "Hit return..."

eps(x, y) = abs (x - dp(y)) + 1.e-14
set yrange [*:+1.]

unset logscale x
set logscale y
plot \
     'tnd.data' index 1 using 1:(eps($2,$1)) title "forward (1)" with lines, \
     ''         index 5 using 1:(eps($2,$1)) title "forward (2)" with lines, \
     ''         index 6 using 1:(eps($2,$1)) title "forward (3)" with lines, \
     ''         index 2 using 1:(eps($2,$1)) title "backward (1)" with lines, \
     ''         index 7 using 1:(eps($2,$1)) title "backward (2)" with lines, \
     ''         index 8 using 1:(eps($2,$1)) title "backward (3)" with lines
#
unset logscale y
pause -1 "Hit return..."

     
# end of test_numerical_differentiation.gpl

