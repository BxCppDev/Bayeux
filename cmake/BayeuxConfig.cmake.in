# - Config file for the Bayeux package
# It defines the following variables
#  Bayeux_INCLUDE_DIRS - include directories for Bayeux
#  Bayeux_DEFINITIONS  - compile definitions needed to use Bayeux
#  Bayeux_LIBRARIES    - libraries to link against

#----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Locate ourselves, since all our config files should have been installed
# alongside us...
#
get_filename_component(__bayeux_config_dir "${CMAKE_CURRENT_LIST_FILE}" PATH)

#-----------------------------------------------------------------------
# Hard code paths to headers for now...
#
get_filename_component(Bayeux_INCLUDE_DIR "${__bayeux_config_dir}/../../../include/" ABSOLUTE CACHE)
set(Bayeux_INCLUDE_DIRS "${Bayeux_INCLUDE_DIR}" "${Bayeux_INCLUDE_DIR}/bayeux")
set(Bayeux_DEFINITIONS)

#-----------------------------------------------------------------------
# Handle paths/targets for third party deps here...
# At present, simply hard code paths that Cadfael will have supplied.
# Basic, but gets the job done for now. Later on we can provide better
# fallback functionality to allow other sources than Cadfael.

# - Boost
find_package(Boost @Boost_VERSION@ EXACT REQUIRED
  COMPONENTS filesystem system serialization iostreams
  NO_MODULE
  PATHS @Boost_DIR@
  NO_DEFAULT_PATH
  )

# - CLHEP
find_package(CLHEP @CLHEP_VERSION@ EXACT REQUIRED
  NO_MODULE
  PATHS @CLHEP_DIR@
  NO_DEFAULT_PATH
  )

# - GSL
find_package(GSL @GSL_VERSION@ EXACT REQUIRED
  NO_MODULE
  PATHS @GSL_DIR@
  NO_DEFAULT_PATH
  )

# - ROOT
find_package(ROOT @ROOT_VERSION@ EXACT REQUIRED
  COMPONENTS RIO Tree
  NO_MODULE
  PATHS @ROOT_DIR@
  NO_DEFAULT_PATH
  )

#-----------------------------------------------------------------------
# Append third party include directories to Bayeux's so exposed
# headers are visible
#
list(APPEND Bayeux_INCLUDE_DIRS
  ${Boost_INCLUDE_DIRS}
  ${CLHEP_INCLUDE_DIRS}
  ${GSL_INCLUDE_DIRS}
  ${ROOT_INCLUDE_DIRS}
  )

#-----------------------------------------------------------------------
# Include the file listing all the imported targets.
# This is installed in the same location as us...
#
if(NOT Bayeux_TARGETS_LOADED)
  include("${__bayeux_config_dir}/BayeuxTargets.cmake")
  set(Bayeux_TARGETS_LOADED 1)
endif()
set(Bayeux_LIBRARY Bayeux CACHE FILE "Bayeux library (imported)")
set(Bayeux_LIBRARIES ${Bayeux_LIBRARY})


